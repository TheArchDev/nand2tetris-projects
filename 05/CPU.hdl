// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // instruction[15] is op code; A-instruction (0) vs C-instruction (1)
    Mux16(a=instruction, b=output-alu, sel=instruction[15], out=input-a-register);

    // write to A-register if i) A-instruction ii) C-instruction with "d1" true
    Not(in=instruction[15], out=a-instruction);
    // instruction[5] is "d1" from dest bits
    And(a=instruction[15], b=instruction[5], out=c-instruction-with-d1);
    Or(a=a-instruction, b=c-instruction-with-d1, out=load-a-register);

    ARegister(in=input-a-register, load=load-a-register, out=output-a-register, out[0..14]=addressM);

    // instruction[12] is "a" from comp bits
    Mux16(a=output-a-register, b=inM, sel=instruction[12], out=input-alu-y);

    // write to D-register if C-instruction with "d2" true
    // instruction[4] is "d2" from dest bits
    And(a=instruction[15], b=instruction[4], out=load-d-register);
    DRegister(in=output-alu, load=load-d-register, out=input-alu-x);

    // write to M-register if C-instruction with "d3" true
    // instruction[3] is "d3" from dest bits
    Mux(a=false, b=instruction[3], sel=instruction[15], out=writeM);

    ALU(
        x=input-alu-x,
        y=input-alu-y,
        zx=instruction[11],
        nx=instruction[10],
        zy=instruction[9],
        ny=instruction[8],
        f=instruction[7],
        no=instruction[6],
        zr=output-alu-zr,
        ng=output-alu-ng,
        out=output-alu,
        out=outM
    );

    // j1 = instruction[2]
    // j2 = instruction[1]
    // j3 = instruction[0]

    // JLT logic for time 12 step
    // if j1=1, and ng=1 (zr value doesn't matter), then load
    And(a=instruction[2], b=output-alu-ng, out=pctemp1);
    
    // JEQ logic for time 17 step
    // if j2=1, and zr=1 (ng doesn't matter, as can't both be 1), then load
    And(a=instruction[1], b=output-alu-zr, out=pctemp2);

    // JGT logic time 40 step
    // if j3=1 only, and zr=ng=0, then load
    Not(in=output-alu-zr, out=tempzr);
    Not(in=output-alu-ng, out=tempng);
    And(a=instruction[0], b=tempzr, out=tempjgt);
    And(a=tempjgt, b=tempng, out=pctemp3);

    Or(a=pctemp1, b=pctemp2, out=pcor1);
    Or(a=pcor1, b=pctemp3, out=load-temp);
    // only do if C-instruction
    And(a=load-temp, b=instruction[15], out=load-pc);
    
    PC(in=output-a-register, load=load-pc, inc=true, reset=reset, out[0..14]=pc);
















}